//adapted from https://learn.microsoft.com/en-us/azure/search/search-query-odata-syntax-reference

?filter_expression: boolean_expression

?boolean_expression: logical_expression
                    | text_expression
                    | comparison_expression
                    | boolean_literal
                    | "(" boolean_expression ")"

logical_expression: boolean_expression logical_operator boolean_expression
                    | "not" boolean_expression -> negation

logical_operator: "and" -> and
                | "or"  -> or

text_expression: NAME text_operator string_literal

text_operator: "contains" -> contains
             | "startswith" -> startswith
             | "endswith" -> endswith


comparison_expression: constant comparison_operator constant

comparison_operator: "eq" -> eq
                   | "gt" -> gt
                   | "lt" -> lt
                   | "ge" -> ge
                   | "le" -> le

?constant: string_literal
         | NUMBER
         | NAME
         | boolean_literal
         | "null" -> null

string_literal: /'[^']*'/
boolean_literal: "true"  -> true 
               | "false" -> false
                
%import common.CNAME -> NAME
%import common.WORD
%import common.NUMBER
%import common.WS
%ignore WS